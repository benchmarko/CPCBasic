/* globals cpcBasic */

"use strict";

cpcBasic.addItem("", function () { /*
10 REM Test Page
20 REM Marco Vieth, 2019-2020
30 '
40 '
50 '|renum,100,100,10,9000:stop
60 '
100 '
110 MODE 2:'comment
120 '
130 PRINT"Numbers"
140 a=1:IF a<>1 THEN ERROR 33
150 a%=1:IF a%<>1 THEN ERROR 33
160 a=1.2:IF a<>1.2 THEN ERROR 33
170 a!=1.2:IF a!<>1.2 THEN ERROR 33
180 a!=-1.2:IF a!<>-1.2 THEN ERROR 33
190 a=1200:IF a<>1200 THEN ERROR 33
200 a%=1200:IF a%<>1200 THEN ERROR 33
210 a=-7.2:IF a<>-7.2 THEN ERROR 33
220 a%=-7.2:IF a%<>-7 THEN ERROR 33
230 a=+7.2:IF a<>7.2 THEN ERROR 33
240 a=0.2:IF a<>0.2 THEN ERROR 33
250 a=2:IF a<>2 THEN ERROR 33
260 a=10000:IF a<>10000 THEN ERROR 33
270 a=0.0001:IF a<>0.0001 THEN ERROR 33
280 PRINT"hex number: &, &h"
290 a=&A7:IF a<>167 THEN ERROR 33
300 a%=&A7:IF a%<>167 THEN ERROR 33
310 a%=-&A7:IF a%<>-167 THEN ERROR 33
320 a%=&7FFF:IF a%<>32767 THEN ERROR 33
330 PRINT"bin number: &x"
340 a=&X10100111:IF a<>167 THEN ERROR 33
350 a%=&X10100111:IF a%<>167 THEN ERROR 33
360 a%=&X111111111111111:IF a%<>32767 THEN ERROR 33
370 a%=-&X111111111111111:IF a%<>-32767 THEN ERROR 33
380 PRINT"Strings"
390 a$="a12":IF a$<>"a12" THEN ERROR 33
400 a$=+"7.1":IF a$<>"7.1" THEN ERROR 33
410 '
420 PRINT"Variable types"
430 a!=1.4:IF a!<>1.4 THEN ERROR 33
440 a!=1.5:IF a!<>1.5 THEN ERROR 33
450 a%=1.4:IF a%<>1 THEN ERROR 33
460 a%=1.5:IF a%<>2 THEN ERROR 33
470 a$="1.4":IF a$<>"1.4" THEN ERROR 33
480 insert.line=2:IF insert.line<>2 THEN ERROR 33
490 '
500 PRINT"Array Variables"
510 a!(2)=1.4:IF a!(2)<>1.4 THEN ERROR 33
520 a!(2)=1.5:IF a!(2)<>1.5 THEN ERROR 33
530 a%(2)=1.4:IF a%(2)<>1 THEN ERROR 33
540 a%(2)=1.5:IF a%(2)<>2 THEN ERROR 33
550 a$(2)="1.4":IF a$(2)<>"1.4" THEN ERROR 33
560 '
570 PRINT"expressions, operators +-*..."
580 a%=1+2+3:IF a%<>6 THEN ERROR 33
590 a%=3-2-1:IF a%<>0 THEN ERROR 33
600 a%=&A7+&X10100111-(123-27):IF a%<>238 THEN ERROR 33
610 a%=3+2*3-7:IF a%<>2 THEN ERROR 33
620 a%=(3+2)*(3-7):IF a%<>-20 THEN ERROR 33
630 a=-(10-7)-(-6-2):IF a<>5 THEN ERROR 33
640 a=20/2.5:IF a<>8 THEN ERROR 33
650 a=20\3:IF a<>6 THEN ERROR 33
660 a=3^2:IF a<>9 THEN ERROR 33
670 a=&X1001 AND &X1110:IF a<>&X1000 THEN ERROR 33
680 a=&X1001 OR &X110:IF a<>&X1111 THEN ERROR 33
690 a=&X1001 XOR &X1010:IF a<>&X11 THEN ERROR 33
700 a=NOT &X1001:IF a<>-10 THEN ERROR 33
710 a=+++++++++---9:IF a<>-9 THEN ERROR 33
720 a=(1=0):IF a<>0 THEN ERROR 33
730 a=(1>0)*(0<1):IF a<>1 THEN ERROR 33
740 GOSUB 9040
750 '
760 PRINT"ABS(positive number)"
770 a=ABS(+67.98):IF a<>67.98 THEN ERROR 33
780 a!=ABS(+67.98):IF a!<>67.98 THEN ERROR 33
790 PRINT"ABS(negative number)"
800 a=ABS(-67.98):IF a<>67.98 THEN ERROR 33
810 PRINT"ABS(0)"
820 a=ABS(0):IF a<>0 THEN ERROR 33
830 '
840 PRINT"@ (address of)": 'CPCBasic: just return internal variable index
850 CLEAR
860 a=7:PRINT@a;@a^2;@a+1*2
870 b=8:PRINT@b;@a(0)
880 '
890 PRINT"AND (and OR)"
900 a=4 OR 7 AND 2:IF a<>6 THEN ERROR 33
910 a%=4 OR 7 AND 2:IF a%<>6 THEN ERROR 33
920 '
930 PRINT"ASC"
940 a=ASC("a"):IF a<>97 THEN ERROR 33
950 a=ASC("ab"):IF a<>97 THEN ERROR 33
960 ON ERROR GOTO 970:a=ASC(""):PRINT"Error expected!":ERROR 33: 'expect error 5
970 IF ERR<>5 THEN PRINT"err=";ERR;"erl=";ERL:ERROR 33 ELSE RESUME 980
980 ON ERROR GOTO 0
990 ON ERROR GOTO 1000:a=ASC(0):PRINT"Error expected!":ERROR 33: 'expect error 13
1000 IF ERR<>13 THEN PRINT"err=";ERR;"erl=";ERL:ERROR 33 ELSE RESUME 1010
1010 ON ERROR GOTO 0
1020 '
1030 'cls#0: a=2: cls #(a*3)
1040 PRINT"COPYCHR$"
1050 PRINT"Detect char 143 with matching pen"
1060 PRINT CHR$(143);"#";:PAPER 1
1070 GOSUB 9010:PAPER 0:IF a$<>CHR$(143)+"#" THEN PRINT"Check: Different on real CPC!":IF a$<>" #" THEN ERROR 33
1080 PRINT"Detect char 130 with matching paper as char 141"
1090 PEN 0:PAPER 1:PRINT CHR$(130);"#";:PEN 1
1100 GOSUB 9010:PAPER 0:IF a$<>CHR$(141)+"#" THEN PRINT"Check: Different on real CPC!":IF a$<>CHR$(130)+"#" THEN ERROR 33
1110 PRINT"Detect char 143 with matching paper as char 32"
1120 PEN 0:PAPER 1:PRINT CHR$(143);"#";:PEN 1
1130 GOSUB 9010:PAPER 0:IF a$<>" #" THEN ERROR 33
1140 '
1150 PRINT"DATA with spaces between arguments"
1160 b$="":RESTORE 1180
1170 READ a$:b$=b$+a$:IF a$<>"-1" THEN GOTO 1170
1180 DATA ",", "abc"  , xy, -1
1190 IF b$<>",abcxy-1" THEN ERROR 33
1200 PRINT"DATA with special characters"
1210 b$="":RESTORE 1220
1220 DATA " ",!"#$%&'()*+,","
1230 FOR i=1 TO 3:READ a$:b$=b$+a$:NEXT
1240 IF b$<>" !"+CHR$(34)+"#$%&'()*+," THEN ERROR 33
1250 PRINT"DATA is interpeted depending on variable type"
1260 DATA 001.6,001.6, 001.6
1270 READ a%,a!,a$
1280 IF a%<>2 THEN ERROR 33
1290 IF a!<>1.6 THEN ERROR 33
1300 IF a$<>"001.6" THEN ERROR 33
1310 DATA &a7, &ha7, &x10100111
1320 READ a%:IF a%<>&A7 THEN ERROR 33
1330 READ a%:IF a%<>&A7 THEN ERROR 33
1340 READ a%:IF a%<>&A7 THEN ERROR 33
1350 PRINT"DATA with empty parameters"
1360 b$="":RESTORE 1460
1370 FOR i=1 TO 16:READ a$:b$=b$+a$:NEXT
1380 IF b$<>"1a11b1#" THEN ERROR 33
1390 PRINT"DATA reading empty as numbers"
1400 b$="":RESTORE 1460
1410 FOR i=1 TO 16
1420 IF i=8 OR i=13 OR i=16 THEN READ a$ ELSE READ a:a$=STR$(a)
1430 b$=b$+a$
1440 NEXT
1450 IF b$<>" 0 0 0 0 0 0 1a 0 1 1 0b 1 0#" THEN ERROR 33
1460 DATA
1470 DATA ,
1480 DATA ,,
1490 DATA 1
1500 DATA a
1510 DATA ,1
1520 DATA 1,
1530 DATA   b   ,   1   ,
1540 DATA #
1550 '
1560 PRINT"DEC$(number,format)"
1570 a$=DEC$(8.575,"##.##"):IF a$<>" 8.58" THEN ERROR 33
1580 a$=DEC$(15.35,"#.##"):IF a$<>"%15.35" THEN ERROR 33
1590 '
1600 PRINT"DEF FN"
1610 DEF FNf1(x)=x*x
1620 a=FNf1(2.5):IF a<>6.25 THEN ERROR 33
1630 a=FN f1(2.5):IF a<>6.25 THEN ERROR 33
1640 DEF FN f1%(x)=x*x
1650 a%=FNf1%(2.5):IF a%<>6 THEN ERROR 33
1660 a%=FN f1%(2.5):IF a%<>6 THEN ERROR 33
1670 a=FNf1%(2.5):IF a<>6 THEN ERROR 33
1680 DEF FN f1!(x)=x*x
1690 a!=FNf1!(2.5):IF a!<>6.25 THEN ERROR 33
1700 a!=FN f1!(2.5):IF a!<>6.25 THEN ERROR 33
1710 a=FNf1!(2.5):IF a<>6.25 THEN ERROR 33
1720 DEF FN f1$(x$)=x$+x$
1730 a$=FNf1$("a"):IF a$<>"aa" THEN ERROR 33
1740 a$=FN f1$("a"):IF a$<>"aa" THEN ERROR 33
1750 DEF FNf2=2.5*2.5
1760 a=FNf2:IF a<>6.25 THEN ERROR 33
1770 DEF FN f2%=2.5*2.5
1780 a%=FN f2%:IF a%<>6 THEN ERROR 33
1790 '
1800 PRINT"DIM"
1810 dim m(0)
1820 m(0)=2
1830 erase m
1840 a=1:dim m(a)
1850 m(0)=2:m(1)=3
1860 erase m
1870 dim m(12,13)
1880 m(12,13)=3
1890 erase m
1900 dim m(1,2)
1910 m(1,2)=4
1920 dim n(m(1,2),15)
1930 FOR i=0 TO m(1,2):n(i,15)=i:NEXT
1940 a=0:FOR i=0 TO m(1,2):a=a+n(i,15):NEXT:IF a<>10 THEN ERROR 33
1950 erase m,n
1960 '
1970 PRINT"ELSE"
1980 a=1 ELSE a=2
1990 ELSE a=3
2000 IF a<>1 THEN ERROR 33
2010 '
2020 PRINT"ERASE"
2030 ERASE a: 'a was used in previous tests
2040 DIM a(4):FOR i=0 TO 4:a(i)=i:NEXT
2050 a=0:FOR i=0 TO 4:a=a+a(i):NEXT:IF a<>10 THEN ERROR 33
2060 ERASE a
2070 IF a<>10 THEN ERROR 33
2080 a=0:FOR i=0 TO 4:a=a+a(i):NEXT:IF a<>0 THEN ERROR 33
2090 GOSUB 9040
2100 '
2110 PRINT"FIX"
2120 a=FIX(1.5)
2130 IF a<>1 THEN ERROR 33
2140 a=FIX(-1.5)
2150 IF a<>-1 THEN ERROR 33
2160 '
2170 PRINT"FOR with integer constants"
2180 a$="":FOR i=+4 TO 0 STEP -2:a$=a$+STR$(i):NEXT:IF a$<>" 4 2 0" THEN ERROR 33
2190 a=0:FOR i=++4 TO 1 STEP ---2:a=a+i:NEXT:IF a<>6 THEN ERROR 33: 'avoid ++ and -- in js!
2200 PRINT"FOR with integer variable and floating point ranges"
2210 a=0:FOR i%=1.2 TO 9.7 STEP 3.2:a=a+i%:NEXT:IF a<>22 THEN ERROR 33: '1+4+7+10
2220 PRINT"FOR with condition expressions"
2230 a=3:FOR i=a<>3 TO a>=3 STEP a=3:PRINT i;:NEXT:PRINT "#";
2240 GOSUB 9010:IF a$<>" 0 -1 #" THEN ERROR 33
2250 PRINT"FOR up to 2*PI"
2260 a=13/8*PI:FOR i=1 TO 3:a=a+1/8*PI:NEXT:IF a>2*PI THEN PRINT"limit exceeded by";a-2*PI;"(TODO)" ELSE PRINT"ok"
2270 'gosub 9040
2280 '
2290 PRINT"GOTO with leading zeros"
2300 GOTO 2310
2310 PRINT"ok"
2320 PRINT"IF"
2330 a=1:IF a=1 THEN GOTO 2350
2340 ERROR 33
2350 IF a=1 THEN 2370
2360 ERROR 33
2370 IF a=1 GOTO 2390
2380 ERROR 33
2390 IF a=1 THEN a=a+1:GOTO 2410
2400 ERROR 33
2410 a=1:IF a=1 THEN 2430:?"dead code":ERROR 33
2420 ERROR 33
2430 a=0:IF a=1 THEN 2440 ELSE 2450
2440 ERROR 33
2450 IF a=1 THEN 2460 ELSE GOTO 2470
2460 ERROR 33
2470 a=1:IF a=1 THEN GOSUB 2490:GOTO 2500
2480 ERROR 33
2490 a=0:RETURN
2500 if a<>0 THEN ERROR 33
2510 PRINT"INSTR"
2520 a=INSTR("Amstrad", "m"):IF a<>2 THEN ERROR 33
2530 a=INSTR("Amstrad", "sr"):IF a<>0 THEN ERROR 33
2540 PRINT"MID$"
2550 a$="abcd":b$=mid$(a$,2):IF b$<>"bcd" THEN ERROR 33
2560 a$="abcd":b$=mid$(a$,2,2):IF b$<>"bc" THEN ERROR 33
2570 PRINT"MID$ as assign"
2580 a$="abcd":b$="xyz":MID$(a$,2)=b$:IF a$<>"axyz" OR b$<>"xyz" THEN ERROR 33
2590 a$="abcd":b$="xyz":MID$(a$,2,2)=b$:IF a$<>"axyd" THEN ERROR 33
2600 PRINT"ON n GOSUB"
2610 a=0:ON 1 GOSUB 2680,2690:IF a<>1 THEN ERROR 33
2620 a=0:ON 2 GOSUB 2680,2690:IF a<>2 THEN ERROR 33
2630 a=0:ON 1.5 GOSUB 2680,2690:IF a<>2 THEN ERROR 33
2640 a=1.5:ON a GOSUB 2680,2690:IF a<>2 THEN ERROR 33
2650 a=0:ON 3 GOSUB 2680,2690:IF a<>0 THEN ERROR 33
2660 a=0:ON 0 GOSUB 2680,2690:IF a<>0 THEN ERROR 33
2670 GOTO 2700
2680 a=1:RETURN
2690 a=2:RETURN
2700 PRINT"ON n GOTO"
2710 a=1.7:ON a-0.2 GOTO 2730,2740
2720 GOTO 2750
2730 a=1:GOTO 2750
2740 a=2:GOTO 2750
2750 IF a<>2 THEN ERROR 33
2760 GOSUB 9040
2770 '
2780 PRINT"PRINT in FOR loop"
2790 FOR i=1 TO 5:PRINT i;:NEXT i:PRINT"#";
2800 GOSUB 9010:IF a$<>" 1  2  3  4  5 #" THEN ERROR 33
2810 PRINT"PRINT in GOTO loop"
2820 a=1
2830 PRINT a;: a=a+1: IF a <= 5 THEN GOTO 2830 ELSE PRINT "#";
2840 GOSUB 9010:IF a$<>" 1  2  3  4  5 #" THEN ERROR 33
2850 PRINT"PRINT in WHILE loop"
2860 a=1: WHILE a<=5: PRINT a;: a=a+1: WEND: PRINT "#";
2870 GOSUB 9010:IF a$<>" 1  2  3  4  5 #" THEN ERROR 33
2880 PRINT"PRINT concatenated string"
2890 a=1: s$="": WHILE a<=5: s$=s$+STR$(a)+":": a=a+1: b=0: WHILE b<3: b=b+1: s$=s$+STR$(b): WEND: s$=s$+" ": WEND: s$=s$+"#":PRINT s$;
2900 GOSUB 9010:IF a$<>" 1: 1 2 3  2: 1 2 3  3: 1 2 3  4: 1 2 3  5: 1 2 3 #" THEN ERROR 33
2910 '
2920 PRINT"IF THEN ELSE: WEND in ELSE"
2930 a=0: WHILE a<5: a=a+1: IF a=3 OR 3=a THEN PRINT "a=";a;"(three) "; ELSE PRINT "a<>3:";a;"(not three) ";: WEND : PRINT"after WEND": 'WEND in ELSE
2940 PRINT"#";
2950 GOSUB 9010:IF a$<>"a<>3: 1 (not three) a<>3: 2 (not three) a= 3 (three) #" THEN ERROR 33
2960 '
2970 GOSUB 9040
2980 PRINT"PRINT numbers separated by space"
2990 PRINT 123;:PRINT"#";
3000 GOSUB 9010: 'if a$<>" 1  2  3 #" then error 33: 'not ok! On real CPC one number: " 123 #"
3010 PRINT"PRINT numbers separated by ;"
3020 PRINT 1;2;3;:PRINT"#";
3030 GOSUB 9010:IF a$<>" 1  2  3 #" THEN ERROR 33
3040 PRINT"PRINT numbers separated by , (default ZONE 13)"
3050 PRINT 1,2,3;:PRINT"#";
3060 GOSUB 9010:IF a$<>" 1            2            3 #" THEN ERROR 33
3070 PRINT"PRINT numbers, computed"
3080 PRINT -1 -2 -3;"#";
3090 GOSUB 9010:IF a$<>"-6 #" THEN ERROR 33
3100 PRINT -1;-2;-3;"#";
3110 GOSUB 9010:IF a$<>"-1 -2 -3 #" THEN ERROR 33
3120 PRINT"PRINT strings separated by space"
3130 PRINT "a" "b" "c" "#";
3140 GOSUB 9010:IF a$<>"abc#" THEN ERROR 33
3150 PRINT"PRINT strings separated by ;"
3160 PRINT "a";"b";"c";"#";
3170 GOSUB 9010:IF a$<>"abc#" THEN ERROR 33
3180 PRINT"PRINT strings separated by ,"
3190 PRINT "a","b","c";"#";: '[zone 13]
3200 GOSUB 9010:IF a$<>"a            b            c#" THEN ERROR 33
3210 ZONE 5
3220 PRINT "a","b","c";"#";: '[zone 5]
3230 GOSUB 9010:IF a$<>"a    b    c#" THEN ERROR 33
3240 ZONE 13
3250 PRINT"PRINT strings separated by tab()"
3260 PRINT "a"TAB(2)"b"TAB(3)"c"TAB(4)"#";
3270 GOSUB 9010:IF a$<>"abc#" THEN ERROR 33
3280 PRINT "a"TAB(13)"b"TAB(20)"c"TAB(22)"#";
3290 GOSUB 9010:IF a$<>"a           b      c #" THEN ERROR 33
3300 PRINT "a"TAB(78)"bc#";
3310 IF POS(#0)<>1 THEN ERROR 33
3320 PRINT CHR$(8);: 'back
3330 IF POS(#0)<>80 THEN ERROR 33
3340 GOSUB 9010:IF a$<>"a                                                                            bc#" THEN ERROR 33
3350 PRINT "a"TAB(79)"bc#";
3360 GOSUB 9010:IF a$<>"bc#" THEN ERROR 33
3370 PRINT"PRINT number without separator"
3380 PRINT &X10 2;"#";
3390 GOSUB 9010:IF a$<>" 2  2 #" THEN ERROR 33
3400 GOSUB 9040
3410 '
3420 PRINT"PRINT special exponential number expressions"
3430 PRINT 1 e++4;:PRINT"#";
3440 GOSUB 9010:IF a$<>" 1  4 #" THEN ERROR 33
3450 PRINT 1 e+-4;:PRINT"#";
3460 GOSUB 9010:IF a$<>" 1 -4 #" THEN ERROR 33
3470 PRINT 50000 0.5;:PRINT"#";
3480 GOSUB 9010: 'IF a$<>" 50000  0.5 #" THEN ERROR 33: 'TODO this one will be put together on a real CPC
3490 PRINT 1 EXP(0);:PRINT"#";
3500 GOSUB 9010:IF a$<>" 1  1 #" THEN ERROR 33
3510 '
3520 PRINT"PRINT USING number format"
3530 PRINT USING "##.##";0;:PRINT"#";
3540 GOSUB 9010:IF a$<>" 0.00#" THEN ERROR 33
3550 PRINT USING "##.##";-1.2;:PRINT"#";
3560 GOSUB 9010:IF a$<>"-1.20#" THEN ERROR 33
3570 PRINT USING "##.##";1.005;:PRINT"#";
3580 GOSUB 9010:IF a$<>" 1.01#" THEN ERROR 33
3590 PRINT USING "##.##";8.575;:PRINT"#";
3600 GOSUB 9010:IF a$<>" 8.58#" THEN ERROR 33
3610 PRINT"PRINT USING number too long"
3620 PRINT USING "#.##";15.355;:PRINT"#";
3630 GOSUB 9010:IF a$<>"%15.36#" THEN ERROR 33
3640 PRINT USING "[#,###,###]";1234567;123;12345678;:PRINT"#";
3650 GOSUB 9010:IF a$<>"[1,234,567][      123][%12,345,678]#" THEN ERROR 33
3660 PRINT"PRINT USING string format"
3670 PRINT USING "\   \";"n1";"n2";" xx3";:PRINT"#";
3680 GOSUB 9010:IF a$<>"n1   n2    xx3 #" THEN ERROR 33
3690 PRINT USING "!";"a1";"b2";:PRINT"#";
3700 GOSUB 9010:IF a$<>"ab#" THEN ERROR 33
3710 PRINT USING "&";"a1";"b2";:PRINT"#";
3720 GOSUB 9010:IF a$<>"a1b2#" THEN ERROR 33
3730 'gosub 9040
3740 '
3750 PRINT"ROUND"
3760 a=ROUND(PI):IF a<>3 THEN ERROR 33
3770 a=ROUND(PI,0):IF a<>3 THEN ERROR 33
3780 a=ROUND(PI,0.4):IF a<>3 THEN ERROR 33
3790 a=ROUND(PI,2):IF a<>3.14 THEN ERROR 33
3800 a=ROUND(PI,2.4):IF a<>3.14 THEN ERROR 33
3810 a=ROUND(1234.5678,-2):IF a<>1200 THEN ERROR 33
3820 a=ROUND(8.575,2):IF a<>8.58 THEN ERROR 33
3830 '
3840 GOSUB 9040
3850 PRINT"DATA and RESTORE"
3860 RESTORE 3870: READ s$,t$: IF s$+t$<>"1" THEN ERROR 33
3870 DATA 1,
3880 '
3890 PRINT"OPENIN and LINE INPUT #9"
3900 t$="testpage.dat":PRINT"OPENIN ";t$;" with characters 33..255"
3910 OPENIN "!"+t$
3920 FOR i=33 TO 255:LINE INPUT #9,t$
3930 t=ASC(t$):PRINT t$;: IF t<>i THEN PRINT"error:";i;"<>";t:ERROR 33
3940 PRINT t$;
3950 NEXT
3960 PRINT
3970 CLOSEIN
3980 PRINT
3990 '
4000 PRINT"OPENOUT, OPENIN and LINE INPUT #9"
4010 OPENOUT "!testpg2.dat"
4020 FOR i=33 TO 255:t$=CHR$(i):PRINT #9,t$
4030 NEXT
4040 CLOSEOUT
4050 OPENIN "!testpg2.dat"
4060 FOR i=33 TO 255:LINE INPUT #9,t$:PRINT t$;
4070 t=ASC(t$):IF t<>i THEN PRINT"error:";i;"<>";t:ERROR 33
4080 NEXT
4090 CLOSEIN
4100 PRINT:PRINT
4110 '
4120 PRINT"Numbers in files"
4130 OPENOUT "!testpg2.dat"
4140 FOR i=0 TO 10:PRINT#9,i:NEXT :'separate lines
4150 CLOSEOUT
4160 OPENIN "!testpg2.dat"
4170 FOR i=0 TO 10:INPUT #9,t
4180 IF i<>t THEN PRINT"error:";i;"<>";t:ERROR 33
4190 NEXT
4200 CLOSEIN
4210 OPENIN "!testpg2.dat"
4220 FOR i=0 TO 10:INPUT #9,t$
4230 a$=STR$(i)+" ":a$=RIGHT$(a$,LEN(a$)-1):IF a$<>t$ THEN PRINT"error:";a$;"<>";t$:ERROR 33
4240 NEXT
4250 CLOSEIN
4260 '
4270 OPENOUT "!testpg2.dat"
4280 FOR i=0 TO 10:PRINT#9,i;:NEXT :'one line
4290 CLOSEOUT
4300 OPENIN "!testpg2.dat"
4310 FOR i=0 TO 10:INPUT #9,t
4320 IF i<>t THEN PRINT"error:";i;"<>";t:ERROR 33
4330 NEXT
4340 CLOSEIN
4350 OPENIN "!testpg2.dat"
4360 INPUT #9,t$
4370 IF t$<>"0  1  2  3  4  5  6  7  8  9  10 " THEN PRINT"error:";:ERROR 33
4380 CLOSEIN
4390 PRINT
4400 '
4410 PRINT"Mixed style"
4420 OPENOUT "!testpg2.dat"
4430 FOR i=0 TO 10:PRINT#9,i;"&a1";&A2:NEXT
4440 CLOSEOUT
4450 OPENIN "!testpg2.dat"
4460 FOR i=0 TO 10:INPUT #9,t,t2
4470 IF i<>t THEN PRINT"error:";i;"<>";t:ERROR 33
4480 IF t2<>&A1 THEN PRINT"error:";t2;"<>";&A1:ERROR 33
4490 INPUT #9,t2
4500 IF t2<>&A2 THEN PRINT"error:";t2;"<>";&A2:ERROR 33
4510 NEXT
4520 CLOSEIN
4530 '
4540 PRINT"Separated with comma"
4550 OPENOUT "!testpg2.dat"
4560 FOR i=0 TO 25:PRINT#9,i+65,CHR$(i+65):NEXT
4570 CLOSEOUT
4580 OPENIN "!testpg2.dat"
4590 FOR i=0 TO 25:INPUT #9,t,t$
4600 IF i+65<>t THEN PRINT"error:";i+65;"<>";t:ERROR 33
4610 IF t$<>CHR$(i+65) THEN PRINT"error:";t$;"<>";CHR$(i+65):ERROR 33
4620 NEXT
4630 CLOSEIN
4640 '
4650 PRINT"WRITE and INPUT"
4660 OPENOUT "!testpg2.dat"
4670 FOR i=0 TO 25:WRITE #9,i+65,CHR$(i+65):NEXT
4680 CLOSEOUT
4690 OPENIN "!testpg2.dat"
4700 FOR i=0 TO 25:INPUT #9,t,t$
4710 IF i+65<>t THEN PRINT"error:";i+65;"<>";t:ERROR 33
4720 IF t$<>CHR$(i+65) THEN PRINT"error:";t$;"<>";CHR$(i+65):ERROR 33
4730 NEXT
4740 CLOSEIN
4750 '
4760 ?"POS(#9)"
4770 OPENOUT"!testpg2.dat":a$=""
4780 FOR i=65 TO 70
4790 a$=a$+CHR$(i)
4800 ?#9,CHR$(13);a$;
4810 ?POS(#9);
4820 NEXT:CLOSEOUT
4830 PRINT"#";
4840 GOSUB 9010:?:IF a$<>" 2  3  4  5  6  7 #" THEN ERROR 33
4850 '
4860 '
4870 PRINT"|ERA to delete testpg2.dat (CPC: DISC only)"
4880 |ERA,"testpg2.dat"
4890 '
4900 GOSUB 9040
4910 '
4920 PRINT"SYMBOL AFTER"
4930 a=240:h=HIMEM+(256-a)*8
4940 a=256:SYMBOL AFTER a:IF HIMEM<>h-(256-a)*8 THEN PRINT"error:";HIMEM;"<>";h:ERROR 33
4950 a=0:SYMBOL AFTER a:IF HIMEM<>h-(256-a)*8 THEN PRINT"error:";HIMEM;"<>";h:ERROR 33
4960 a=240:SYMBOL AFTER a:IF HIMEM<>h-(256-a)*8 THEN PRINT"error:";HIMEM;"<>";h:ERROR 33
4970 MEMORY HIMEM-1
4980 ON ERROR GOTO 4990:SYMBOL AFTER 241:PRINT"Error expected!":ERROR 33: 'expect error 5
4990 IF ERR<>5 THEN PRINT"err=";ERR;"erl=";ERL:ERROR 33 ELSE RESUME 5000
5000 ON ERROR GOTO 0
5010 MEMORY HIMEM+1
5020 PRINT"UNT"
5030 a=UNT(32767):IF a<>32767 THEN ERROR 33
5040 a=UNT(32768):IF a<>-32768 THEN ERROR 33
5050 a=UNT(65535):IF a<>-1 THEN ERROR 33
5060 PRINT"VAL"
5070 a=VAL(""):IF a<>0 THEN ERROR 33
5080 a=VAL("4r"):IF a<>4 THEN ERROR 33
5090 a=VAL("&ff"):IF a<>&FF THEN ERROR 33
5100 '
5110 GOSUB 9040
5120 '
5130 PRINT "stairs"
5140 FOR i=1 TO 24:PRINT STRING$(i*2, "O"):NEXT
5150 MOVE 0,350
5160 FOR n=1 TO 8
5170 DRAWR 50,0
5180 DRAWR 0,-50
5190 NEXT
5200 MOVE 348,0
5210 FILL 1
5220 '
5230 PRINT "test finished: ok"
5240 END
5250 '
9000 'get characters from screen; print crlf
9010 a$="":i=1:WHILE i<=80 AND RIGHT$(a$,1)<>"#":LOCATE i,VPOS(#0):a$=a$+COPYCHR$(#0):i=i+1:WEND:PRINT:RETURN
9020 '
9030 'wait some time or for key press
9040 t!=TIME+6*50:a$="":WHILE TIME<t! AND a$="":a$=INKEY$:WEND
9050 PRINT:PRINT STRING$(10, "-"):PRINT
9060 RETURN
9070 '
*/ });
