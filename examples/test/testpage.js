/* globals cpcBasic */

"use strict";

cpcBasic.addItem("", function () { /*
10 REM Test Page
20 REM Marco Vieth, 2019-2020
30 '
40 '
50 '|renum,100,100,10,9000:stop
60 '
100 '
110 MODE 2:'comment
120 '
130 PRINT"Numbers"
140 a=1:IF a<>1 THEN ERROR 33
150 a%=1:IF a%<>1 THEN ERROR 33
160 a=1.2:IF a<>1.2 THEN ERROR 33
170 a!=1.2:IF a!<>1.2 THEN ERROR 33
180 a!=-1.2:IF a!<>-1.2 THEN ERROR 33
190 a=1200:IF a<>1200 THEN ERROR 33
200 a%=1200:IF a%<>1200 THEN ERROR 33
210 a=-7.2:IF a<>-7.2 THEN ERROR 33
220 a%=-7.2:IF a%<>-7 THEN ERROR 33
230 a=+7.2:IF a<>7.2 THEN ERROR 33
240 a=0.2:IF a<>0.2 THEN ERROR 33
250 a=2:IF a<>2 THEN ERROR 33
260 a=10000:IF a<>10000 THEN ERROR 33
270 a=0.0001:IF a<>0.0001 THEN ERROR 33
280 PRINT"hex number: &, &h"
290 a=&A7:IF a<>167 THEN ERROR 33
300 a%=&A7:IF a%<>167 THEN ERROR 33
310 a%=-&A7:IF a%<>-167 THEN ERROR 33
320 a%=&7FFF:IF a%<>32767 THEN ERROR 33
330 PRINT"bin number: &x"
340 a=&X10100111:IF a<>167 THEN ERROR 33
350 a%=&X10100111:IF a%<>167 THEN ERROR 33
360 a%=&X111111111111111:IF a%<>32767 THEN ERROR 33
370 a%=-&X111111111111111:IF a%<>-32767 THEN ERROR 33
380 PRINT"Strings"
390 a$="a12":IF a$<>"a12" THEN ERROR 33
400 a$=+"7.1":IF a$<>"7.1" THEN ERROR 33
410 '
420 PRINT"Variable types"
430 a!=1.4:IF a!<>1.4 THEN ERROR 33
440 a!=1.5:IF a!<>1.5 THEN ERROR 33
450 a%=1.4:IF a%<>1 THEN ERROR 33
460 a%=1.5:IF a%<>2 THEN ERROR 33
470 a$="1.4":IF a$<>"1.4" THEN ERROR 33
480 insert.line=2:IF insert.line<>2 THEN ERROR 33
490 '
500 PRINT"Array Variables"
510 a!(2)=1.4:IF a!(2)<>1.4 THEN ERROR 33
520 a!(2)=1.5:IF a!(2)<>1.5 THEN ERROR 33
530 a%(2)=1.4:IF a%(2)<>1 THEN ERROR 33
540 a%(2)=1.5:IF a%(2)<>2 THEN ERROR 33
550 a$(2)="1.4":IF a$(2)<>"1.4" THEN ERROR 33
560 '
570 PRINT"expressions, operators +-*..."
580 a%=1+2+3:IF a%<>6 THEN ERROR 33
590 a%=3-2-1:IF a%<>0 THEN ERROR 33
600 a%=&A7+&X10100111-(123-27):IF a%<>238 THEN ERROR 33
610 a%=3+2*3-7:IF a%<>2 THEN ERROR 33
620 a%=(3+2)*(3-7):IF a%<>-20 THEN ERROR 33
622 a=-(10-7)-(-6-2):IF a<>5 THEN ERROR 33
630 a=20/2.5:IF a<>8 THEN ERROR 33
640 a=20\3:IF a<>6 THEN ERROR 33
650 a=3^2:IF a<>9 THEN ERROR 33
660 a=&X1001 AND &X1110:IF a<>&X1000 THEN ERROR 33
670 a=&X1001 OR &X110:IF a<>&X1111 THEN ERROR 33
680 a=&X1001 XOR &X1010:IF a<>&X11 THEN ERROR 33
681 a=NOT &X1001:IF a<>-10 THEN ERROR 33
690 a=+++++++++---9:IF a<>-9 THEN ERROR 33
700 a=(1=0):IF a<>0 THEN ERROR 33
710 a=(1>0)*(0<1):IF a<>1 THEN ERROR 33
720 GOSUB 9040
730 '
740 PRINT"ABS(positive number)"
750 a=ABS(+67.98):IF a<>67.98 THEN ERROR 33
760 a!=ABS(+67.98):IF a!<>67.98 THEN ERROR 33
770 PRINT"ABS(negative number)"
780 a=ABS(-67.98):IF a<>67.98 THEN ERROR 33
790 PRINT"ABS(0)"
800 a=ABS(0):IF a<>0 THEN ERROR 33
810 '
820 PRINT"@ (address of)": 'CPCBasic: just return internal variable index
830 CLEAR
840 a=7:PRINT@a;@a^2;@a+1*2
850 b=8:PRINT@b;@a(0)
860 '
870 PRINT"AND (and OR)"
880 a=4 OR 7 AND 2:IF a<>6 THEN ERROR 33
890 a%=4 OR 7 AND 2:IF a%<>6 THEN ERROR 33
900 '
910 PRINT"ASC"
920 a=ASC("a"):IF a<>97 THEN ERROR 33
930 a=ASC("ab"):IF a<>97 THEN ERROR 33
940 ON ERROR GOTO 950:a=ASC(""):PRINT"Error expected!":ERROR 33: 'expect error 5
950 IF ERR<>5 THEN PRINT"err=";ERR;"erl=";ERL:ERROR 33 ELSE RESUME 960
960 ON ERROR GOTO 0
970 ON ERROR GOTO 980:a=ASC(0):PRINT"Error expected!":ERROR 33: 'expect error 13
980 IF ERR<>13 THEN PRINT"err=";ERR;"erl=";ERL:ERROR 33 ELSE RESUME 990
990 ON ERROR GOTO 0
1000 '
1010 'cls#0: a=2: cls #(a*3)
1020 PRINT"COPYCHR$"
1030 PRINT"Detect char 143 with matching pen"
1040 PRINT CHR$(143);"#";:PAPER 1
1050 GOSUB 9010:PAPER 0:IF a$<>CHR$(143)+"#" THEN PRINT"Check: Different on real CPC!":IF a$<>" #" THEN ERROR 33
1060 PRINT"Detect char 130 with matching paper as char 141"
1070 PEN 0:PAPER 1:PRINT CHR$(130);"#";:PEN 1
1080 GOSUB 9010:PAPER 0:IF a$<>CHR$(141)+"#" THEN PRINT"Check: Different on real CPC!":IF a$<>CHR$(130)+"#" THEN ERROR 33
1090 PRINT"Detect char 143 with matching paper as char 32"
1100 PEN 0:PAPER 1:PRINT CHR$(143);"#";:PEN 1
1110 GOSUB 9010:PAPER 0:IF a$<>" #" THEN ERROR 33
1120 '
1130 PRINT"DATA with spaces between arguments"
1140 b$="":RESTORE 1160
1150 READ a$:b$=b$+a$:IF a$<>"-1" THEN GOTO 1150
1160 DATA ",", "abc"  , xy, -1
1170 IF b$<>",abcxy-1" THEN ERROR 33
1180 PRINT"DATA with special characters"
1190 b$="":RESTORE 1200
1200 DATA " ",!"#$%&'()*+,","
1210 FOR i=1 TO 3:READ a$:b$=b$+a$:NEXT
1220 IF b$<>" !"+CHR$(34)+"#$%&'()*+," THEN ERROR 33
1230 PRINT"DATA is interpeted depending on variable type"
1240 DATA 001.6,001.6, 001.6
1250 READ a%,a!,a$
1260 IF a%<>2 THEN ERROR 33
1270 IF a!<>1.6 THEN ERROR 33
1280 IF a$<>"001.6" THEN ERROR 33
1290 DATA &a7, &ha7, &x10100111
1300 READ a%:IF a%<>&A7 THEN ERROR 33
1310 READ a%:IF a%<>&A7 THEN ERROR 33
1320 READ a%:IF a%<>&A7 THEN ERROR 33
1330 PRINT"DATA with empty parameters"
1340 b$="":RESTORE 1440
1350 FOR i=1 TO 16:READ a$:b$=b$+a$:NEXT
1360 IF b$<>"1a11b1#" THEN ERROR 33
1370 PRINT"DATA reading empty as numbers"
1380 b$="":RESTORE 1440
1390 FOR i=1 TO 16
1400 IF i=8 OR i=13 OR i=16 THEN READ a$ ELSE READ a:a$=STR$(a)
1410 b$=b$+a$
1420 NEXT
1430 IF b$<>" 0 0 0 0 0 0 1a 0 1 1 0b 1 0#" THEN ERROR 33
1440 DATA
1450 DATA ,
1460 DATA ,,
1470 DATA 1
1480 DATA a
1490 DATA ,1
1500 DATA 1,
1510 DATA   b   ,   1   ,
1520 DATA #
1530 '
1540 PRINT"DEC$(number,format)"
1550 a$=DEC$(8.575,"##.##"):IF a$<>" 8.58" THEN ERROR 33
1560 a$=DEC$(15.35,"#.##"):IF a$<>"%15.35" THEN ERROR 33
1570 '
1580 PRINT"DEF FN"
1590 DEF FNf1(x)=x*x
1600 a=FNf1(2.5):IF a<>6.25 THEN ERROR 33
1610 a=FN f1(2.5):IF a<>6.25 THEN ERROR 33
1620 DEF FN f1%(x)=x*x
1630 a%=FNf1%(2.5):IF a%<>6 THEN ERROR 33
1640 a%=FN f1%(2.5):IF a%<>6 THEN ERROR 33
1650 a=FNf1%(2.5):IF a<>6 THEN ERROR 33
1660 DEF FN f1!(x)=x*x
1670 a!=FNf1!(2.5):IF a!<>6.25 THEN ERROR 33
1680 a!=FN f1!(2.5):IF a!<>6.25 THEN ERROR 33
1690 a=FNf1!(2.5):IF a<>6.25 THEN ERROR 33
1700 DEF FN f1$(x$)=x$+x$
1710 a$=FNf1$("a"):IF a$<>"aa" THEN ERROR 33
1720 a$=FN f1$("a"):IF a$<>"aa" THEN ERROR 33
1730 DEF FNf2=2.5*2.5
1740 a=FNf2:IF a<>6.25 THEN ERROR 33
1750 DEF FN f2%=2.5*2.5
1760 a%=FN f2%:IF a%<>6 THEN ERROR 33
1770 '
1780 PRINT"DIM"
1790 dim m(0)
1800 m(0)=2
1810 erase m
1820 a=1:dim m(a)
1830 m(0)=2:m(1)=3
1840 erase m
1850 dim m(12,13)
1860 m(12,13)=3
1862 erase m
1863 dim m(1,2)
1864 m(1,2)=4
1870 dim n(m(1,2),15)
1880 FOR i=0 TO m(1,2):n(i,15)=i:NEXT
1890 a=0:FOR i=0 TO m(1,2):a=a+n(i,15):NEXT:IF a<>10 THEN ERROR 33
1900 erase m,n
1910 '
1920 PRINT"ELSE"
1930 a=1 ELSE a=2
1940 ELSE a=3
1950 IF a<>1 THEN ERROR 33
1960 '
1970 PRINT"ERASE"
1980 ERASE a: 'a was used in previous tests
1990 DIM a(4):FOR i=0 TO 4:a(i)=i:NEXT
2000 a=0:FOR i=0 TO 4:a=a+a(i):NEXT:IF a<>10 THEN ERROR 33
2010 ERASE a
2020 IF a<>10 THEN ERROR 33
2030 a=0:FOR i=0 TO 4:a=a+a(i):NEXT:IF a<>0 THEN ERROR 33
2040 GOSUB 9040
2050 '
2060 PRINT"FIX"
2070 a=FIX(1.5)
2080 IF a<>1 THEN ERROR 33
2090 a=FIX(-1.5)
2100 IF a<>-1 THEN ERROR 33
2110 '
2120 PRINT"FOR with integer constants"
2130 a$="":FOR i=+4 TO 0 STEP -2:a$=a$+STR$(i):NEXT:IF a$<>" 4 2 0" THEN ERROR 33
2140 a=0:FOR i=++4 TO 1 STEP ---2:a=a+i:NEXT:IF a<>6 THEN ERROR 33: 'avoid ++ and -- in js!
2150 PRINT"FOR with integer variable and floating point ranges"
2160 a=0:FOR i%=1.2 TO 9.7 STEP 3.2:a=a+i%:NEXT:IF a<>22 THEN ERROR 33: '1+4+7+10
2170 PRINT"FOR with condition expressions"
2180 a=3:FOR i=a<>3 TO a>=3 STEP a=3:PRINT i;:NEXT:PRINT "#";
2190 GOSUB 9010:IF a$<>" 0 -1 #" THEN ERROR 33
2200 PRINT"FOR up to 2*PI"
2210 a=13/8*PI:FOR i=1 TO 3:a=a+1/8*PI:NEXT:IF a>2*PI THEN PRINT"limit exceeded by";a-2*PI;"(TODO)" ELSE PRINT"ok"
2220 'gosub 9040
2230 '
2240 PRINT"GOTO with leading zeros"
2250 GOTO 2260
2260 PRINT"ok"
2261 PRINT"IF"
2262 a=1:IF a=1 THEN GOTO 2264
2263 ERROR 33
2264 IF a=1 THEN 2266
2265 ERROR 33
2266 IF a=1 GOTO 2268
2267 ERROR 33
2268 IF a=1 THEN a=a+1:GOTO 2270
2269 ERROR 33
2270 a=1:IF a=1 THEN 2273:?"dead code":ERROR 33
2271 ERROR 33
2273 a=0:IF a=1 THEN 2274 ELSE 2275
2274 ERROR 33
2275 IF a=1 THEN 2276 ELSE GOTO 2277
2276 ERROR 33
2277 a=1:IF a=1 THEN GOSUB 2280:GOTO 2281
2279 ERROR 33
2280 a=0:RETURN
2281 if a<>0 THEN ERROR 33
2285 PRINT"INSTR"
2286 a=INSTR("Amstrad", "m"):IF a<>2 THEN ERROR 33
2290 a=INSTR("Amstrad", "sr"):IF a<>0 THEN ERROR 33
2291 PRINT"MID$"
2292 a$="abcd":b$=mid$(a$,2):IF b$<>"bcd" THEN ERROR 33
2293 a$="abcd":b$=mid$(a$,2,2):IF b$<>"bc" THEN ERROR 33
2295 PRINT"MID$ as assign"
2296 a$="abcd":b$="xyz":MID$(a$,2)=b$:IF a$<>"axyz" OR b$<>"xyz" THEN ERROR 33
2297 a$="abcd":b$="xyz":MID$(a$,2,2)=b$:IF a$<>"axyd" THEN ERROR 33
2310 PRINT"ON n GOSUB"
2320 a=0:ON 1 GOSUB 2390,2400:IF a<>1 THEN ERROR 33
2330 a=0:ON 2 GOSUB 2390,2400:IF a<>2 THEN ERROR 33
2340 a=0:ON 1.5 GOSUB 2390,2400:IF a<>2 THEN ERROR 33
2350 a=1.5:ON a GOSUB 2390,2400:IF a<>2 THEN ERROR 33
2360 a=0:ON 3 GOSUB 2390,2400:IF a<>0 THEN ERROR 33
2370 a=0:ON 0 GOSUB 2390,2400:IF a<>0 THEN ERROR 33
2380 GOTO 2410
2390 a=1:RETURN
2400 a=2:RETURN
2410 PRINT"ON n GOTO"
2420 a=1.7:ON a-0.2 GOTO 2440,2450
2430 GOTO 2460
2440 a=1:GOTO 2460
2450 a=2:GOTO 2460
2460 IF a<>2 THEN ERROR 33
2470 GOSUB 9040
2480 '
2490 PRINT"PRINT in FOR loop"
2500 FOR i=1 TO 5:PRINT i;:NEXT i:PRINT"#";
2510 GOSUB 9010:IF a$<>" 1  2  3  4  5 #" THEN ERROR 33
2520 PRINT"PRINT in GOTO loop"
2530 a=1
2540 PRINT a;: a=a+1: IF a <= 5 THEN GOTO 2540 ELSE PRINT "#";
2550 GOSUB 9010:IF a$<>" 1  2  3  4  5 #" THEN ERROR 33
2560 PRINT"PRINT in WHILE loop"
2570 a=1: WHILE a<=5: PRINT a;: a=a+1: WEND: PRINT "#";
2580 GOSUB 9010:IF a$<>" 1  2  3  4  5 #" THEN ERROR 33
2590 PRINT"PRINT concatenated string"
2600 a=1: s$="": WHILE a<=5: s$=s$+STR$(a)+":": a=a+1: b=0: WHILE b<3: b=b+1: s$=s$+STR$(b): WEND: s$=s$+" ": WEND: s$=s$+"#":PRINT s$;
2610 GOSUB 9010:IF a$<>" 1: 1 2 3  2: 1 2 3  3: 1 2 3  4: 1 2 3  5: 1 2 3 #" THEN ERROR 33
2620 '
2630 PRINT"IF THEN ELSE: WEND in ELSE"
2640 a=0: WHILE a<5: a=a+1: IF a=3 OR 3=a THEN PRINT "a=";a;"(three) "; ELSE PRINT "a<>3:";a;"(not three) ";: WEND : PRINT"after WEND": 'WEND in ELSE
2650 PRINT"#";
2660 GOSUB 9010:IF a$<>"a<>3: 1 (not three) a<>3: 2 (not three) a= 3 (three) #" THEN ERROR 33
2670 '
2680 GOSUB 9040
2690 PRINT"PRINT numbers separated by space"
2700 PRINT 123;:PRINT"#";
2710 GOSUB 9010: 'if a$<>" 1  2  3 #" then error 33: 'not ok! On real CPC one number: " 123 #"
2720 PRINT"PRINT numbers separated by ;"
2730 PRINT 1;2;3;:PRINT"#";
2740 GOSUB 9010:IF a$<>" 1  2  3 #" THEN ERROR 33
2750 PRINT"PRINT numbers separated by , (default ZONE 13)"
2760 PRINT 1,2,3;:PRINT"#";
2770 GOSUB 9010:IF a$<>" 1            2            3 #" THEN ERROR 33
2780 PRINT"PRINT numbers, computed"
2790 PRINT -1 -2 -3;"#";
2800 GOSUB 9010:IF a$<>"-6 #" THEN ERROR 33
2810 PRINT -1;-2;-3;"#";
2820 GOSUB 9010:IF a$<>"-1 -2 -3 #" THEN ERROR 33
2830 PRINT"PRINT strings separated by space"
2840 PRINT "a" "b" "c" "#";
2850 GOSUB 9010:IF a$<>"abc#" THEN ERROR 33
2860 PRINT"PRINT strings separated by ;"
2870 PRINT "a";"b";"c";"#";
2880 GOSUB 9010:IF a$<>"abc#" THEN ERROR 33
2890 PRINT"PRINT strings separated by ,"
2900 PRINT "a","b","c";"#";: '[zone 13]
2910 GOSUB 9010:IF a$<>"a            b            c#" THEN ERROR 33
2920 ZONE 5
2930 PRINT "a","b","c";"#";: '[zone 5]
2940 GOSUB 9010:IF a$<>"a    b    c#" THEN ERROR 33
2950 ZONE 13
2960 PRINT"PRINT strings separated by tab()"
2970 PRINT "a"TAB(2)"b"TAB(3)"c"TAB(4)"#";
2980 GOSUB 9010:IF a$<>"abc#" THEN ERROR 33
2990 PRINT "a"TAB(13)"b"TAB(20)"c"TAB(22)"#";
3000 GOSUB 9010:IF a$<>"a           b      c #" THEN ERROR 33
3010 PRINT "a"TAB(78)"bc#";
3020 IF POS(#0)<>1 THEN ERROR 33
3030 PRINT CHR$(8);: 'back
3040 IF POS(#0)<>80 THEN ERROR 33
3050 GOSUB 9010:IF a$<>"a                                                                            bc#" THEN ERROR 33
3060 PRINT "a"TAB(79)"bc#";
3070 GOSUB 9010:IF a$<>"bc#" THEN ERROR 33
3080 PRINT"PRINT number without separator"
3090 PRINT &X10 2;"#";
3100 GOSUB 9010:IF a$<>" 2  2 #" THEN ERROR 33
3110 GOSUB 9040
3120 '
3130 PRINT"PRINT special exponential number expressions"
3140 PRINT 1 e++4;:PRINT"#";
3150 GOSUB 9010:IF a$<>" 1  4 #" THEN ERROR 33
3160 PRINT 1 e+-4;:PRINT"#";
3170 GOSUB 9010:IF a$<>" 1 -4 #" THEN ERROR 33
3180 PRINT 50000 0.5;:PRINT"#";
3190 GOSUB 9010: 'IF a$<>" 50000  0.5 #" THEN ERROR 33: 'TODO this one will be put together on a real CPC
3200 PRINT 1 EXP(0);:PRINT"#";
3210 GOSUB 9010:IF a$<>" 1  1 #" THEN ERROR 33
3220 '
3230 PRINT"PRINT USING number format"
3240 PRINT USING "##.##";0;:PRINT"#";
3250 GOSUB 9010:IF a$<>" 0.00#" THEN ERROR 33
3260 PRINT USING "##.##";-1.2;:PRINT"#";
3270 GOSUB 9010:IF a$<>"-1.20#" THEN ERROR 33
3280 PRINT USING "##.##";1.005;:PRINT"#";
3290 GOSUB 9010:IF a$<>" 1.01#" THEN ERROR 33
3300 PRINT USING "##.##";8.575;:PRINT"#";
3310 GOSUB 9010:IF a$<>" 8.58#" THEN ERROR 33
3320 PRINT"PRINT USING number too long"
3330 PRINT USING "#.##";15.355;:PRINT"#";
3340 GOSUB 9010:IF a$<>"%15.36#" THEN ERROR 33
3350 PRINT USING "[#,###,###]";1234567;123;12345678;:PRINT"#";
3360 GOSUB 9010:IF a$<>"[1,234,567][      123][%12,345,678]#" THEN ERROR 33
3370 PRINT"PRINT USING string format"
3380 PRINT USING "\   \";"n1";"n2";" xx3";:PRINT"#";
3390 GOSUB 9010:IF a$<>"n1   n2    xx3 #" THEN ERROR 33
3400 PRINT USING "!";"a1";"b2";:PRINT"#";
3410 GOSUB 9010:IF a$<>"ab#" THEN ERROR 33
3420 PRINT USING "&";"a1";"b2";:PRINT"#";
3430 GOSUB 9010:IF a$<>"a1b2#" THEN ERROR 33
3440 'gosub 9040
3450 '
3460 PRINT"ROUND"
3470 a=ROUND(PI):IF a<>3 THEN ERROR 33
3480 a=ROUND(PI,0):IF a<>3 THEN ERROR 33
3490 a=ROUND(PI,0.4):IF a<>3 THEN ERROR 33
3500 a=ROUND(PI,2):IF a<>3.14 THEN ERROR 33
3510 a=ROUND(PI,2.4):IF a<>3.14 THEN ERROR 33
3520 a=ROUND(1234.5678,-2):IF a<>1200 THEN ERROR 33
3530 a=ROUND(8.575,2):IF a<>8.58 THEN ERROR 33
3540 '
3550 GOSUB 9040
3560 PRINT"DATA and RESTORE"
3570 RESTORE 3580: READ s$,t$: IF s$+t$<>"1" THEN ERROR 33
3580 DATA 1,
3590 '
3600 PRINT"OPENIN and LINE INPUT #9"
3610 t$="testpage.dat":PRINT"OPENIN ";t$;" with characters 33..255"
3620 OPENIN "!"+t$
3630 FOR i=33 TO 255:LINE INPUT #9,t$
3640 t=ASC(t$):PRINT t$;: IF t<>i THEN PRINT"error:";i;"<>";t:ERROR 33
3650 PRINT t$;
3660 NEXT
3670 PRINT
3680 CLOSEIN
3690 PRINT
3700 '
3710 PRINT"OPENOUT, OPENIN and LINE INPUT #9"
3720 OPENOUT "!testpg2.dat"
3730 FOR i=33 TO 255:t$=CHR$(i):PRINT #9,t$
3740 NEXT
3750 CLOSEOUT
3760 OPENIN "!testpg2.dat"
3770 FOR i=33 TO 255:LINE INPUT #9,t$:PRINT t$;
3780 t=ASC(t$):IF t<>i THEN PRINT"error:";i;"<>";t:ERROR 33
3790 NEXT
3800 CLOSEIN
3810 PRINT:PRINT
3820 '
3830 PRINT"Numbers in files"
3840 OPENOUT "!testpg2.dat"
3850 FOR i=0 TO 10:PRINT#9,i:NEXT :'separate lines
3860 CLOSEOUT
3870 OPENIN "!testpg2.dat"
3880 FOR i=0 TO 10:INPUT #9,t
3890 IF i<>t THEN PRINT"error:";i;"<>";t:ERROR 33
3900 NEXT
3910 CLOSEIN
3920 OPENIN "!testpg2.dat"
3930 FOR i=0 TO 10:INPUT #9,t$
3940 a$=STR$(i)+" ":a$=RIGHT$(a$,LEN(a$)-1):IF a$<>t$ THEN PRINT"error:";a$;"<>";t$:ERROR 33
3950 NEXT
3960 CLOSEIN
3970 '
3980 OPENOUT "!testpg2.dat"
3990 FOR i=0 TO 10:PRINT#9,i;:NEXT :'one line
4000 CLOSEOUT
4010 OPENIN "!testpg2.dat"
4020 FOR i=0 TO 10:INPUT #9,t
4030 IF i<>t THEN PRINT"error:";i;"<>";t:ERROR 33
4040 NEXT
4050 CLOSEIN
4060 OPENIN "!testpg2.dat"
4070 INPUT #9,t$
4080 IF t$<>"0  1  2  3  4  5  6  7  8  9  10 " THEN PRINT"error:";:ERROR 33
4090 CLOSEIN
4100 PRINT
4110 '
4120 PRINT"Mixed style"
4130 OPENOUT "!testpg2.dat"
4140 FOR i=0 TO 10:PRINT#9,i;"&a1";&A2:NEXT
4150 CLOSEOUT
4160 OPENIN "!testpg2.dat"
4170 FOR i=0 TO 10:INPUT #9,t,t2
4180 IF i<>t THEN PRINT"error:";i;"<>";t:ERROR 33
4190 IF t2<>&A1 THEN PRINT"error:";t2;"<>";&A1:ERROR 33
4200 INPUT #9,t2
4210 IF t2<>&A2 THEN PRINT"error:";t2;"<>";&A2:ERROR 33
4220 NEXT
4230 CLOSEIN
4240 '
4250 PRINT"Separated with comma"
4260 OPENOUT "!testpg2.dat"
4270 FOR i=0 TO 25:PRINT#9,i+65,CHR$(i+65):NEXT
4280 CLOSEOUT
4290 OPENIN "!testpg2.dat"
4300 FOR i=0 TO 25:INPUT #9,t,t$
4310 IF i+65<>t THEN PRINT"error:";i+65;"<>";t:ERROR 33
4320 IF t$<>CHR$(i+65) THEN PRINT"error:";t$;"<>";CHR$(i+65):ERROR 33
4330 NEXT
4340 CLOSEIN
4350 '
4360 PRINT"WRITE and INPUT"
4370 OPENOUT "!testpg2.dat"
4380 FOR i=0 TO 25:WRITE #9,i+65,CHR$(i+65):NEXT
4390 CLOSEOUT
4400 OPENIN "!testpg2.dat"
4410 FOR i=0 TO 25:INPUT #9,t,t$
4420 IF i+65<>t THEN PRINT"error:";i+65;"<>";t:ERROR 33
4430 IF t$<>CHR$(i+65) THEN PRINT"error:";t$;"<>";CHR$(i+65):ERROR 33
4440 NEXT
4450 CLOSEIN
4460 '
4470 '
4480 PRINT"|ERA to delete testpg2.dat (CPC: DISC only)"
4490 |ERA,"testpg2.dat"
4500 '
4510 GOSUB 9040
4520 '
4530 PRINT"SYMBOL AFTER"
4540 a=240:h=HIMEM+(256-a)*8
4550 a=256:SYMBOL AFTER a:IF HIMEM<>h-(256-a)*8 THEN PRINT"error:";HIMEM;"<>";h:ERROR 33
4560 a=0:SYMBOL AFTER a:IF HIMEM<>h-(256-a)*8 THEN PRINT"error:";HIMEM;"<>";h:ERROR 33
4570 a=240:SYMBOL AFTER a:IF HIMEM<>h-(256-a)*8 THEN PRINT"error:";HIMEM;"<>";h:ERROR 33
4580 MEMORY HIMEM-1
4590 ON ERROR GOTO 4600:SYMBOL AFTER 241:PRINT"Error expected!":ERROR 33: 'expect error 5
4600 IF ERR<>5 THEN PRINT"err=";ERR;"erl=";ERL:ERROR 33 ELSE RESUME 4610
4610 ON ERROR GOTO 0
4620 MEMORY HIMEM+1
4630 PRINT"UNT"
4640 a=UNT(32767):IF a<>32767 THEN ERROR 33
4650 a=UNT(32768):IF a<>-32768 THEN ERROR 33
4660 a=UNT(65535):IF a<>-1 THEN ERROR 33
4670 PRINT"VAL"
4680 a=VAL(""):IF a<>0 THEN ERROR 33
4690 a=VAL("4r"):IF a<>4 THEN ERROR 33
4700 a=VAL("&ff"):IF a<>&FF THEN ERROR 33
4710 '
4720 GOSUB 9040
4730 '
4740 PRINT "stairs"
4750 FOR i=1 TO 24:PRINT STRING$(i*2, "O"):NEXT
4760 MOVE 0,350
4770 FOR n=1 TO 8
4780 DRAWR 50,0
4790 DRAWR 0,-50
4800 NEXT
4810 MOVE 348,0
4820 FILL 1
4830 '
4840 PRINT "test finished: ok"
4850 END
4860 '
9000 'get characters from screen; print crlf
9010 a$="":i=1:WHILE i<=80 AND RIGHT$(a$,1)<>"#":LOCATE i,VPOS(#0):a$=a$+COPYCHR$(#0):i=i+1:WEND:PRINT:RETURN
9020 '
9030 'wait some time or for key press
9040 t!=TIME+6*50:a$="":WHILE TIME<t! AND a$="":a$=INKEY$:WEND
9050 PRINT:PRINT STRING$(10, "-"):PRINT
9060 RETURN
9070 '
*/ });
