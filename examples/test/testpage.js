/* globals cpcBasic */

"use strict";

cpcBasic.addItem("", function () { /*
10 REM Test Page
20 REM Marco Vieth, 2019-2020
30 '
40 '
50 '|renum,100,100,10,9000:stop
60 '
100 '
110 MODE 2:'comment
120 '
130 PRINT"Numbers"
140 a=1:IF a<>1 THEN ERROR 33
150 a%=1:IF a%<>1 THEN ERROR 33
160 a=1.2:IF a<>1.2 THEN ERROR 33
170 a!=1.2:IF a!<>1.2 THEN ERROR 33
180 a!=-1.2:IF a!<>-1.2 THEN ERROR 33
190 a=1200:IF a<>1200 THEN ERROR 33
200 a%=1200:IF a%<>1200 THEN ERROR 33
210 a=-7.2:IF a<>-7.2 THEN ERROR 33
220 a%=-7.2:IF a%<>-7 THEN ERROR 33
230 a=+7.2:IF a<>7.2 THEN ERROR 33
240 a=0.2:IF a<>0.2 THEN ERROR 33
250 a=2:IF a<>2 THEN ERROR 33
260 a=10000:IF a<>10000 THEN ERROR 33
270 a=0.0001:IF a<>0.0001 THEN ERROR 33
280 PRINT"hex number: &, &h"
290 a=&A7:IF a<>167 THEN ERROR 33
300 a%=&A7:IF a%<>167 THEN ERROR 33
310 a%=-&A7:IF a%<>-167 THEN ERROR 33
320 a%=&7FFF:IF a%<>32767 THEN ERROR 33
330 a=&A000:IF a<>-24576 THEN ERROR 33
340 PRINT"bin number: &x"
350 a=&X10100111:IF a<>167 THEN ERROR 33
360 a%=&X10100111:IF a%<>167 THEN ERROR 33
370 a%=&X111111111111111:IF a%<>32767 THEN ERROR 33
380 a%=-&X111111111111111:IF a%<>-32767 THEN ERROR 33
390 PRINT"Strings"
400 a$="a12":IF a$<>"a12" THEN ERROR 33
410 a$=+"7.1":IF a$<>"7.1" THEN ERROR 33
420 '
430 PRINT"Variable types"
440 a!=1.4:IF a!<>1.4 THEN ERROR 33
450 a!=1.5:IF a!<>1.5 THEN ERROR 33
460 a%=1.4:IF a%<>1 THEN ERROR 33
470 a%=1.5:IF a%<>2 THEN ERROR 33
480 a$="1.4":IF a$<>"1.4" THEN ERROR 33
490 insert.line=2:IF insert.line<>2 THEN ERROR 33
500 '
510 PRINT"Array Variables"
520 a!(2)=1.4:IF a!(2)<>1.4 THEN ERROR 33
530 a!(2)=1.5:IF a!(2)<>1.5 THEN ERROR 33
540 a%(2)=1.4:IF a%(2)<>1 THEN ERROR 33
550 a%(2)=1.5:IF a%(2)<>2 THEN ERROR 33
560 a$(2)="1.4":IF a$(2)<>"1.4" THEN ERROR 33
570 '
580 PRINT"expressions, operators +-*..."
590 a%=1+2+3:IF a%<>6 THEN ERROR 33
600 a%=3-2-1:IF a%<>0 THEN ERROR 33
610 a%=&A7+&X10100111-(123-27):IF a%<>238 THEN ERROR 33
620 a%=3+2*3-7:IF a%<>2 THEN ERROR 33
630 a%=(3+2)*(3-7):IF a%<>-20 THEN ERROR 33
640 a=-(10-7)-(-6-2):IF a<>5 THEN ERROR 33
650 a=20/2.5:IF a<>8 THEN ERROR 33
660 a=20\3:IF a<>6 THEN ERROR 33
670 a=3^2:IF a<>9 THEN ERROR 33
680 a=&X1001 AND &X1110:IF a<>&X1000 THEN ERROR 33
690 a=&X1001 OR &X110:IF a<>&X1111 THEN ERROR 33
700 a=&X1001 XOR &X1010:IF a<>&X11 THEN ERROR 33
710 a=NOT &X1001:IF a<>-10 THEN ERROR 33
720 a=+++++++++---9:IF a<>-9 THEN ERROR 33
730 a=(1=0):IF a<>0 THEN ERROR 33
740 a=(1>0)*(0<1):IF a<>1 THEN ERROR 33
750 a=1=1=-1:IF a<>-1 THEN ERROR 33
760 GOSUB 9040
770 '
780 PRINT"ABS(positive number)"
790 a=ABS(+67.98):IF a<>67.98 THEN ERROR 33
800 a!=ABS(+67.98):IF a!<>67.98 THEN ERROR 33
810 PRINT"ABS(negative number)"
820 a=ABS(-67.98):IF a<>67.98 THEN ERROR 33
830 PRINT"ABS(0)"
840 a=ABS(0):IF a<>0 THEN ERROR 33
850 '
860 PRINT"@ (address of)": 'CPCBasic: just return internal variable index
870 CLEAR
880 a=7:PRINT@a;@a^2;@a+1*2
890 b=8:PRINT@b;@a(0)
900 '
910 PRINT"AND (and OR)"
920 a=4 OR 7 AND 2:IF a<>6 THEN ERROR 33
930 a%=4 OR 7 AND 2:IF a%<>6 THEN ERROR 33
940 '
950 PRINT"ASC"
960 a=ASC("a"):IF a<>97 THEN ERROR 33
970 a=ASC("ab"):IF a<>97 THEN ERROR 33
980 ON ERROR GOTO 990:a=ASC(""):PRINT"Error expected!":ERROR 33: 'expect error 5
990 IF ERR<>5 THEN PRINT"err=";ERR;"erl=";ERL:ERROR 33 ELSE RESUME 1000
1000 ON ERROR GOTO 0
1010 ON ERROR GOTO 1020:a=ASC(0):PRINT"Error expected!":ERROR 33: 'expect error 13
1020 IF ERR<>13 THEN PRINT"err=";ERR;"erl=";ERL:ERROR 33 ELSE RESUME 1030
1030 ON ERROR GOTO 0
1040 '
1050 PRINT "ATN"
1060 RAD:a=INT(ATN(1)*100000000)/100000000:IF a<>0.78539816 THEN ERROR 33
1070 DEG:a=INT(ATN(1)*100000000)/100000000:RAD:IF STR$(a)<>" 45" THEN ERROR 33
1080 DEG:a=INT(ATN(TAN(45))*100000000)/100000000:RAD:IF STR$(a)<>" 45" THEN ERROR 33
1090 '
1100 PRINT "BIN$"
1110 b$=BIN$(0):IF b$<>"0" THEN ERROR 33
1120 b$=BIN$(255):IF b$<>"11111111" THEN ERROR 33
1130 b$=BIN$(255,10):IF b$<>"0011111111" THEN ERROR 33
1140 b$=BIN$(170,6):IF b$<>"10101010" THEN ERROR 33
1150 b$=BIN$(32767,16):IF b$<>"0111111111111111" THEN ERROR 33
1160 b$=BIN$(65535):IF b$<>"1111111111111111" THEN ERROR 33
1170 '
1180 'cls#0: a=2: cls #(a*3)
1190 PRINT"COPYCHR$"
1200 PRINT"Detect char 143 with matching pen"
1210 PRINT CHR$(143);"#";:PAPER 1
1220 GOSUB 9010:PAPER 0:IF a$<>CHR$(143)+"#" THEN PRINT"Check: Different on real CPC!":IF a$<>" #" THEN ERROR 33
1230 PRINT"Detect char 130 with matching paper as char 141"
1240 PEN 0:PAPER 1:PRINT CHR$(130);"#";:PEN 1
1250 GOSUB 9010:PAPER 0:IF a$<>CHR$(141)+"#" THEN PRINT"Check: Different on real CPC!":IF a$<>CHR$(130)+"#" THEN ERROR 33
1260 PRINT"Detect char 143 with matching paper as char 32"
1270 PEN 0:PAPER 1:PRINT CHR$(143);"#";:PEN 1
1280 GOSUB 9010:PAPER 0:IF a$<>" #" THEN ERROR 33
1290 '
1300 PRINT"DATA with spaces between arguments"
1310 b$="":RESTORE 1330
1320 READ a$:b$=b$+a$:IF a$<>"-1" THEN GOTO 1320
1330 DATA ",", "abc"  , xy, -1
1340 IF b$<>",abcxy-1" THEN ERROR 33
1350 PRINT"DATA with special characters"
1360 b$="":RESTORE 1370
1370 DATA " ",!"#$%&'()*+,","
1380 FOR i=1 TO 3:READ a$:b$=b$+a$:NEXT
1390 IF b$<>" !"+CHR$(34)+"#$%&'()*+," THEN ERROR 33
1400 PRINT"DATA is interpeted depending on variable type"
1410 DATA 001.6,001.6, 001.6
1420 READ a%,a!,a$
1430 IF a%<>2 THEN ERROR 33
1440 IF a!<>1.6 THEN ERROR 33
1450 IF a$<>"001.6" THEN ERROR 33
1460 DATA &a7, &ha7, &x10100111
1470 READ a%:IF a%<>&A7 THEN ERROR 33
1480 READ a%:IF a%<>&A7 THEN ERROR 33
1490 READ a%:IF a%<>&A7 THEN ERROR 33
1500 DATA &A000, &A000
1510 READ a%:IF a%<>-24576 OR a%<>&A000 THEN ERROR 33
1520 READ a$:IF a$<>"&A000" THEN ERROR 33
1530 PRINT"DATA with empty parameters"
1540 b$="":RESTORE 1640
1550 FOR i=1 TO 16:READ a$:b$=b$+a$:NEXT
1560 IF b$<>"1a11b1#" THEN ERROR 33
1570 PRINT"DATA reading empty as numbers"
1580 b$="":RESTORE 1640
1590 FOR i=1 TO 16
1600 IF i=8 OR i=13 OR i=16 THEN READ a$ ELSE READ a:a$=STR$(a)
1610 b$=b$+a$
1620 NEXT
1630 IF b$<>" 0 0 0 0 0 0 1a 0 1 1 0b 1 0#" THEN ERROR 33
1640 DATA
1650 DATA ,
1660 DATA ,,
1670 DATA 1
1680 DATA a
1690 DATA ,1
1700 DATA 1,
1710 DATA   b   ,   1   ,
1720 DATA #
1730 '
1740 PRINT"DEC$(number,format)"
1750 a$=DEC$(8.575,"##.##"):IF a$<>" 8.58" THEN ERROR 33
1760 a$=DEC$(15.35,"#.##"):IF a$<>"%15.35" THEN ERROR 33
1770 '
1780 PRINT"DEF FN"
1790 DEF FNf1(x)=x*x
1800 a=FNf1(2.5):IF a<>6.25 THEN ERROR 33
1810 a=FN f1(2.5):IF a<>6.25 THEN ERROR 33
1820 DEF FN f1%(x)=x*x
1830 a%=FNf1%(2.5):IF a%<>6 THEN ERROR 33
1840 a%=FN f1%(2.5):IF a%<>6 THEN ERROR 33
1850 a=FNf1%(2.5):IF a<>6 THEN ERROR 33
1860 DEF FN f1!(x)=x*x
1870 a!=FNf1!(2.5):IF a!<>6.25 THEN ERROR 33
1880 a!=FN f1!(2.5):IF a!<>6.25 THEN ERROR 33
1890 a=FNf1!(2.5):IF a<>6.25 THEN ERROR 33
1900 DEF FN f1$(x$)=x$+x$
1910 a$=FNf1$("a"):IF a$<>"aa" THEN ERROR 33
1920 a$=FN f1$("a"):IF a$<>"aa" THEN ERROR 33
1930 DEF FNf2=2.5*2.5
1940 a=FNf2:IF a<>6.25 THEN ERROR 33
1950 DEF FN f2%=2.5*2.5
1960 a%=FN f2%:IF a%<>6 THEN ERROR 33
1970 '
1980 PRINT"DIM"
1990 dim m(0)
2000 m(0)=2
2010 erase m
2020 a=1:dim m(a)
2030 m(0)=2:m(1)=3
2040 erase m
2050 dim m(12,13)
2060 m(12,13)=3
2070 erase m
2080 dim m(1,2)
2090 m(1,2)=4
2100 dim n(m(1,2),15)
2110 FOR i=0 TO m(1,2):n(i,15)=i:NEXT
2120 a=0:FOR i=0 TO m(1,2):a=a+n(i,15):NEXT:IF a<>10 THEN ERROR 33
2130 erase m,n
2140 '
2150 PRINT"ELSE"
2160 a=1 ELSE a=2
2170 ELSE a=3
2180 IF a<>1 THEN ERROR 33
2190 '
2200 PRINT"ERASE"
2210 ERASE a: 'a was used in previous tests
2220 DIM a(4):FOR i=0 TO 4:a(i)=i:NEXT
2230 a=0:FOR i=0 TO 4:a=a+a(i):NEXT:IF a<>10 THEN ERROR 33
2240 ERASE a
2250 IF a<>10 THEN ERROR 33
2260 a=0:FOR i=0 TO 4:a=a+a(i):NEXT:IF a<>0 THEN ERROR 33
2270 GOSUB 9040
2280 '
2290 PRINT"FIX"
2300 a=FIX(1.5)
2310 IF a<>1 THEN ERROR 33
2320 a=FIX(-1.5)
2330 IF a<>-1 THEN ERROR 33
2340 '
2350 PRINT"FOR with integer constants"
2360 a$="":FOR i=+4 TO 0 STEP -2:a$=a$+STR$(i):NEXT:IF a$<>" 4 2 0" THEN ERROR 33
2370 a=0:FOR i=++4 TO 1 STEP ---2:a=a+i:NEXT:IF a<>6 THEN ERROR 33: 'avoid ++ and -- in js!
2380 PRINT"FOR with integer variable and floating point ranges"
2390 a=0:FOR i%=1.2 TO 9.7 STEP 3.2:a=a+i%:NEXT:IF a<>22 THEN ERROR 33: '1+4+7+10
2400 PRINT"FOR with condition expressions"
2410 a=3:FOR i=a<>3 TO a>=3 STEP a=3:PRINT i;:NEXT:PRINT "#";
2420 GOSUB 9010:IF a$<>" 0 -1 #" THEN ERROR 33
2430 PRINT"FOR up to 2*PI"
2440 a=13/8*PI:FOR i=1 TO 3:a=a+1/8*PI:NEXT:IF a>2*PI THEN PRINT"limit exceeded by";a-2*PI;"(TODO)" ELSE PRINT"ok"
2450 'gosub 9040
2460 '
2470 PRINT"GOTO with leading zeros"
2480 GOTO 2490
2490 PRINT"ok"
2500 PRINT"IF"
2510 a=1:IF a=1 THEN GOTO 2530
2520 ERROR 33
2530 IF a=1 THEN 2550
2540 ERROR 33
2550 IF a=1 GOTO 2570
2560 ERROR 33
2570 IF a=1 THEN a=a+1:GOTO 2590
2580 ERROR 33
2590 a=1:IF a=1 THEN 2610:?"dead code":ERROR 33
2600 ERROR 33
2610 a=0:IF a=1 THEN 2620 ELSE 2630
2620 ERROR 33
2630 IF a=1 THEN 2640 ELSE GOTO 2650
2640 ERROR 33
2650 a=1:IF a=1 THEN GOSUB 2670:GOTO 2680
2660 ERROR 33
2670 a=0:RETURN
2680 if a<>0 THEN ERROR 33
2690 PRINT"INSTR"
2700 a=INSTR("Amstrad", "m"):IF a<>2 THEN ERROR 33
2710 a=INSTR("Amstrad", "sr"):IF a<>0 THEN ERROR 33
2720 a=INSTR(6,"amstrad", "a"):IF a<>6 THEN ERROR 33
2730 PRINT"LOG"
2740 a=LOG(1000)/LOG(10): IF INT(a+1E-9)<>3 THEN ERROR 33
2750 PRINT"LOG10"
2760 a=LOG10(1000):IF a<>3 THEN ERROR 33
2762 PRINT "MAX"
2763 a=MAX(7):IF a<>7 THEN ERROR 33
2764 a=MAX(1.5,2.1,2.0):IF a<>2.1 THEN ERROR 33
2765 a$=MAX("abc"):IF a$<>"abc" THEN ERROR 33
2770 PRINT "MEMORY"
2780 h=HIMEM
2790 MEMORY &A000:IF HIMEM<>40960 THEN ERROR 33
2800 MEMORY -24576:IF HIMEM<>40960 THEN ERROR 33
2810 MEMORY 40960:IF HIMEM<>40960 THEN ERROR 33
2820 MEMORY h
2830 PRINT"MID$"
2840 a$="abcd":b$=mid$(a$,2):IF b$<>"bcd" THEN ERROR 33
2850 a$="abcd":b$=mid$(a$,2,2):IF b$<>"bc" THEN ERROR 33
2860 PRINT"MID$ as assign"
2870 a$="abcd":b$="xyz":MID$(a$,2)=b$:IF a$<>"axyz" OR b$<>"xyz" THEN ERROR 33
2880 a$="abcd":b$="xyz":MID$(a$,2,2)=b$:IF a$<>"axyd" THEN ERROR 33
2882 PRINT "MIN"
2883 a=MIN(7):IF a<>7 THEN ERROR 33
2884 a=MIN(1.5,2.1,2.0):IF a<>1.5 THEN ERROR 33
2885 a$=MIN("abc"):IF a$<>"abc" THEN ERROR 33
2890 PRINT"ON n GOSUB"
2900 a=0:ON 1 GOSUB 2970,2980:IF a<>1 THEN ERROR 33
2910 a=0:ON 2 GOSUB 2970,2980:IF a<>2 THEN ERROR 33
2920 a=0:ON 1.5 GOSUB 2970,2980:IF a<>2 THEN ERROR 33
2930 a=1.5:ON a GOSUB 2970,2980:IF a<>2 THEN ERROR 33
2940 a=0:ON 3 GOSUB 2970,2980:IF a<>0 THEN ERROR 33
2950 a=0:ON 0 GOSUB 2970,2980:IF a<>0 THEN ERROR 33
2960 GOTO 2990
2970 a=1:RETURN
2980 a=2:RETURN
2990 PRINT"ON n GOTO"
3000 a=1.7:ON a-0.2 GOTO 3020,3030
3010 GOTO 3040
3020 a=1:GOTO 3040
3030 a=2:GOTO 3040
3040 IF a<>2 THEN ERROR 33
3050 GOSUB 9040
3060 '
3070 PRINT"PRINT in FOR loop"
3080 FOR i=1 TO 5:PRINT i;:NEXT i:PRINT"#";
3090 GOSUB 9010:IF a$<>" 1  2  3  4  5 #" THEN ERROR 33
3100 FOR i=&FFFC to &3:PRINT i;:NEXT:PRINT"#";
3110 GOSUB 9010:IF a$<>"-4 -3 -2 -1  0  1  2  3 #" THEN ERROR 33
3120 PRINT"PRINT in GOTO loop"
3130 a=1
3140 PRINT a;: a=a+1: IF a <= 5 THEN GOTO 3140 ELSE PRINT "#";
3150 GOSUB 9010:IF a$<>" 1  2  3  4  5 #" THEN ERROR 33
3160 PRINT"PRINT in WHILE loop"
3170 a=1: WHILE a<=5: PRINT a;: a=a+1: WEND: PRINT "#";
3180 GOSUB 9010:IF a$<>" 1  2  3  4  5 #" THEN ERROR 33
3190 PRINT"PRINT concatenated string"
3200 a=1: s$="": WHILE a<=5: s$=s$+STR$(a)+":": a=a+1: b=0: WHILE b<3: b=b+1: s$=s$+STR$(b): WEND: s$=s$+" ": WEND: s$=s$+"#":PRINT s$;
3210 GOSUB 9010:IF a$<>" 1: 1 2 3  2: 1 2 3  3: 1 2 3  4: 1 2 3  5: 1 2 3 #" THEN ERROR 33
3220 '
3230 PRINT"IF THEN ELSE: WEND in ELSE"
3240 a=0: WHILE a<5: a=a+1: IF a=3 OR 3=a THEN PRINT "a=";a;"(three) "; ELSE PRINT "a<>3:";a;"(not three) ";: WEND : PRINT"after WEND": 'WEND in ELSE
3250 PRINT"#";
3260 GOSUB 9010:IF a$<>"a<>3: 1 (not three) a<>3: 2 (not three) a= 3 (three) #" THEN ERROR 33
3270 '
3280 GOSUB 9040
3290 PRINT"PRINT numbers separated by space"
3300 PRINT 123;:PRINT"#";
3310 GOSUB 9010: 'if a$<>" 1  2  3 #" then error 33: 'not ok! On real CPC one number: " 123 #"
3320 PRINT"PRINT numbers separated by ;"
3330 PRINT 1;2;3;:PRINT"#";
3340 GOSUB 9010:IF a$<>" 1  2  3 #" THEN ERROR 33
3350 PRINT"PRINT numbers separated by , (default ZONE 13)"
3360 PRINT 1,2,3;:PRINT"#";
3370 GOSUB 9010:IF a$<>" 1            2            3 #" THEN ERROR 33
3380 PRINT"PRINT numbers, computed"
3390 PRINT -1 -2 -3;"#";
3400 GOSUB 9010:IF a$<>"-6 #" THEN ERROR 33
3410 PRINT -1;-2;-3;"#";
3420 GOSUB 9010:IF a$<>"-1 -2 -3 #" THEN ERROR 33
3430 PRINT"PRINT strings separated by space"
3440 PRINT "a" "b" "c" "#";
3450 GOSUB 9010:IF a$<>"abc#" THEN ERROR 33
3460 PRINT"PRINT strings separated by ;"
3470 PRINT "a";"b";"c";"#";
3480 GOSUB 9010:IF a$<>"abc#" THEN ERROR 33
3490 PRINT"PRINT strings separated by ,"
3500 PRINT "a","b","c";"#";: '[zone 13]
3510 GOSUB 9010:IF a$<>"a            b            c#" THEN ERROR 33
3520 ZONE 5
3530 PRINT "a","b","c";"#";: '[zone 5]
3540 GOSUB 9010:IF a$<>"a    b    c#" THEN ERROR 33
3550 ZONE 13
3560 PRINT"PRINT strings separated by tab()"
3570 PRINT "a"TAB(2)"b"TAB(3)"c"TAB(4)"#";
3580 GOSUB 9010:IF a$<>"abc#" THEN ERROR 33
3590 PRINT "a"TAB(13)"b"TAB(20)"c"TAB(22)"#";
3600 GOSUB 9010:IF a$<>"a           b      c #" THEN ERROR 33
3610 PRINT "a"TAB(78)"bc#";
3620 IF POS(#0)<>1 THEN ERROR 33
3630 PRINT CHR$(8);: 'back
3640 IF POS(#0)<>80 THEN ERROR 33
3650 GOSUB 9010:IF a$<>"a                                                                            bc#" THEN ERROR 33
3660 PRINT "a"TAB(79)"bc#";
3670 GOSUB 9010:IF a$<>"bc#" THEN ERROR 33
3680 PRINT"PRINT number without separator"
3690 PRINT &X10 2;"#";
3700 GOSUB 9010:IF a$<>" 2  2 #" THEN ERROR 33
3710 GOSUB 9040
3720 '
3730 PRINT"PRINT special exponential number expressions"
3740 PRINT 1 e++4;:PRINT"#";
3750 GOSUB 9010:IF a$<>" 1  4 #" THEN ERROR 33
3760 PRINT 1 e+-4;:PRINT"#";
3770 GOSUB 9010:IF a$<>" 1 -4 #" THEN ERROR 33
3780 PRINT 50000 0.5;:PRINT"#";
3790 GOSUB 9010: 'IF a$<>" 50000  0.5 #" THEN ERROR 33: 'TODO this one will be put together on a real CPC
3800 PRINT 1 EXP(0);:PRINT"#";
3810 GOSUB 9010:IF a$<>" 1  1 #" THEN ERROR 33
3820 '
3830 PRINT"PRINT USING number format"
3840 PRINT USING "##.##";0;:PRINT"#";
3850 GOSUB 9010:IF a$<>" 0.00#" THEN ERROR 33
3860 PRINT USING "##.##";-1.2;:PRINT"#";
3870 GOSUB 9010:IF a$<>"-1.20#" THEN ERROR 33
3880 PRINT USING "##.##";1.005;:PRINT"#";
3890 GOSUB 9010:IF a$<>" 1.01#" THEN ERROR 33
3900 PRINT USING "##.##";8.575;:PRINT"#";
3910 GOSUB 9010:IF a$<>" 8.58#" THEN ERROR 33
3920 PRINT"PRINT USING number too long"
3930 PRINT USING "#.##";15.355;:PRINT"#";
3940 GOSUB 9010:IF a$<>"%15.36#" THEN ERROR 33
3950 PRINT USING "[#,###,###]";1234567;123;12345678;:PRINT"#";
3960 GOSUB 9010:IF a$<>"[1,234,567][      123][%12,345,678]#" THEN ERROR 33
3970 PRINT"PRINT USING string format"
3980 PRINT USING "\   \";"n1";"n2";" xx3";:PRINT"#";
3990 GOSUB 9010:IF a$<>"n1   n2    xx3 #" THEN ERROR 33
4000 PRINT USING "!";"a1";"b2";:PRINT"#";
4010 GOSUB 9010:IF a$<>"ab#" THEN ERROR 33
4020 PRINT USING "&";"a1";"b2";:PRINT"#";
4030 GOSUB 9010:IF a$<>"a1b2#" THEN ERROR 33
4040 'gosub 9040
4050 '
4060 PRINT"ROUND"
4070 a=ROUND(PI):IF a<>3 THEN ERROR 33
4080 a=ROUND(PI,0):IF a<>3 THEN ERROR 33
4090 a=ROUND(PI,0.4):IF a<>3 THEN ERROR 33
4100 a=ROUND(PI,2):IF a<>3.14 THEN ERROR 33
4110 a=ROUND(PI,2.4):IF a<>3.14 THEN ERROR 33
4120 a=ROUND(1234.5678,-2):IF a<>1200 THEN ERROR 33
4130 a=ROUND(8.575,2):IF a<>8.58 THEN ERROR 33
4140 '
4150 GOSUB 9040
4160 PRINT"DATA and RESTORE"
4170 RESTORE 4180: READ s$,t$: IF s$+t$<>"1" THEN ERROR 33
4180 DATA 1,
4190 '
4200 PRINT "TAN"
4210 RAD:a=INT(TAN(0.7853981635)*100000000)/100000000:IF a<>1 THEN ERROR 33
4220 DEG:a=INT(TAN(45)*100000000+0.00000001)/100000000:RAD:IF a<>1 THEN ERROR 33
4230 '
4240 PRINT"OPENIN and LINE INPUT #9"
4250 t$="testpage.dat":PRINT"OPENIN ";t$;" with characters 33..255"
4260 OPENIN "!"+t$
4270 FOR i=33 TO 255:LINE INPUT #9,t$
4280 t=ASC(t$):PRINT t$;: IF t<>i THEN PRINT"error:";i;"<>";t:ERROR 33
4290 PRINT t$;
4300 NEXT
4310 PRINT
4320 CLOSEIN
4330 PRINT
4340 '
4350 PRINT"OPENOUT, OPENIN and LINE INPUT #9"
4360 OPENOUT "!testpg2.dat"
4370 FOR i=33 TO 255:t$=CHR$(i):PRINT #9,t$
4380 NEXT
4390 CLOSEOUT
4400 OPENIN "!testpg2.dat"
4410 FOR i=33 TO 255:LINE INPUT #9,t$:PRINT t$;
4420 t=ASC(t$):IF t<>i THEN PRINT"error:";i;"<>";t:ERROR 33
4430 NEXT
4440 CLOSEIN
4450 PRINT:PRINT
4460 '
4470 PRINT"Numbers in files"
4480 OPENOUT "!testpg2.dat"
4490 FOR i=0 TO 10:PRINT#9,i:NEXT :'separate lines
4500 CLOSEOUT
4510 OPENIN "!testpg2.dat"
4520 FOR i=0 TO 10:INPUT #9,t
4530 IF i<>t THEN PRINT"error:";i;"<>";t:ERROR 33
4540 NEXT
4550 CLOSEIN
4560 OPENIN "!testpg2.dat"
4570 FOR i=0 TO 10:INPUT #9,t$
4580 a$=STR$(i)+" ":a$=RIGHT$(a$,LEN(a$)-1):IF a$<>t$ THEN PRINT"error:";a$;"<>";t$:ERROR 33
4590 NEXT
4600 CLOSEIN
4610 '
4620 OPENOUT "!testpg2.dat"
4630 FOR i=0 TO 10:PRINT#9,i;:NEXT :'one line
4640 CLOSEOUT
4650 OPENIN "!testpg2.dat"
4660 FOR i=0 TO 10:INPUT #9,t
4670 IF i<>t THEN PRINT"error:";i;"<>";t:ERROR 33
4680 NEXT
4690 CLOSEIN
4700 OPENIN "!testpg2.dat"
4710 INPUT #9,t$
4720 IF t$<>"0  1  2  3  4  5  6  7  8  9  10 " THEN PRINT"error:";:ERROR 33
4730 CLOSEIN
4740 PRINT
4750 '
4760 PRINT"Mixed style"
4770 OPENOUT "!testpg2.dat"
4780 FOR i=0 TO 10:PRINT#9,i;"&a1";&A2:NEXT
4790 CLOSEOUT
4800 OPENIN "!testpg2.dat"
4810 FOR i=0 TO 10:INPUT #9,t,t2
4820 IF i<>t THEN PRINT"error:";i;"<>";t:ERROR 33
4830 IF t2<>&A1 THEN PRINT"error:";t2;"<>";&A1:ERROR 33
4840 INPUT #9,t2
4850 IF t2<>&A2 THEN PRINT"error:";t2;"<>";&A2:ERROR 33
4860 NEXT
4870 CLOSEIN
4880 '
4890 PRINT"Separated with comma"
4900 OPENOUT "!testpg2.dat"
4910 FOR i=0 TO 25:PRINT#9,i+65,CHR$(i+65):NEXT
4920 CLOSEOUT
4930 OPENIN "!testpg2.dat"
4940 FOR i=0 TO 25:INPUT #9,t,t$
4950 IF i+65<>t THEN PRINT"error:";i+65;"<>";t:ERROR 33
4960 IF t$<>CHR$(i+65) THEN PRINT"error:";t$;"<>";CHR$(i+65):ERROR 33
4970 NEXT
4980 CLOSEIN
4990 '
5000 PRINT"WRITE and INPUT"
5010 OPENOUT "!testpg2.dat"
5020 FOR i=0 TO 25:WRITE #9,i+65,CHR$(i+65):NEXT
5030 CLOSEOUT
5040 OPENIN "!testpg2.dat"
5050 FOR i=0 TO 25:INPUT #9,t,t$
5060 IF i+65<>t THEN PRINT"error:";i+65;"<>";t:ERROR 33
5070 IF t$<>CHR$(i+65) THEN PRINT"error:";t$;"<>";CHR$(i+65):ERROR 33
5080 NEXT
5090 CLOSEIN
5100 '
5110 ?"POS(#9)"
5120 OPENOUT"!testpg2.dat":a$=""
5130 FOR i=65 TO 70
5140 a$=a$+CHR$(i)
5150 ?#9,CHR$(13);a$;
5160 ?POS(#9);
5170 NEXT:CLOSEOUT
5180 PRINT"#";
5190 GOSUB 9010:?:IF a$<>" 2  3  4  5  6  7 #" THEN ERROR 33
5200 '
5210 '
5220 PRINT"|ERA to delete testpg2.dat (CPC: DISC only)"
5230 |ERA,"testpg2.dat"
5240 '
5250 GOSUB 9040
5260 '
5270 PRINT"SYMBOL AFTER"
5280 a=240:h=HIMEM+(256-a)*8
5290 a=256:SYMBOL AFTER a:IF HIMEM<>h-(256-a)*8 THEN PRINT"error:";HIMEM;"<>";h:ERROR 33
5300 a=0:SYMBOL AFTER a:IF HIMEM<>h-(256-a)*8 THEN PRINT"error:";HIMEM;"<>";h:ERROR 33
5310 a=240:SYMBOL AFTER a:IF HIMEM<>h-(256-a)*8 THEN PRINT"error:";HIMEM;"<>";h:ERROR 33
5320 MEMORY HIMEM-1
5330 ON ERROR GOTO 5340:SYMBOL AFTER 241:PRINT"Error expected!":ERROR 33: 'expect error 5
5340 IF ERR<>5 THEN PRINT"err=";ERR;"erl=";ERL:ERROR 33 ELSE RESUME 5350
5350 ON ERROR GOTO 0
5360 MEMORY HIMEM+1
5370 PRINT"UNT"
5380 a=UNT(32767):IF a<>32767 THEN ERROR 33
5390 a=UNT(32768):IF a<>-32768 THEN ERROR 33
5400 a=UNT(65535):IF a<>-1 THEN ERROR 33
5410 PRINT"VAL"
5420 a=VAL(""):IF a<>0 THEN ERROR 33
5430 a=VAL("4r"):IF a<>4 THEN ERROR 33
5440 a=VAL("&ff"):IF a<>&FF THEN ERROR 33
5450 a=VAL("&A000"):IF a<>-24576 or a<>&A000 or a<>&hA000 THEN ERROR 33
5460 a=VAL("&ha000"):IF a<>-24576 or a<>&a000 or a<>&ha000 THEN ERROR 33
5470 '
5480 GOSUB 9040
5490 '
5500 PRINT "stairs"
5510 FOR i=1 TO 24:PRINT STRING$(i*2, "O"):NEXT
5520 MOVE 0,350
5530 FOR n=1 TO 8
5540 DRAWR 50,0
5550 DRAWR 0,-50
5560 NEXT
5570 MOVE 348,0
5580 FILL 1
5590 '
5600 PRINT "test finished: ok"
5610 END
5620 '
9000 'get characters from screen; print crlf
9010 a$="":i=1:WHILE i<=80 AND RIGHT$(a$,1)<>"#":LOCATE i,VPOS(#0):a$=a$+COPYCHR$(#0):i=i+1:WEND:PRINT:RETURN
9020 '
9030 'wait some time or for key press
9040 t!=TIME+6*50:a$="":WHILE TIME<t! AND a$="":a$=INKEY$:WEND
9050 PRINT:PRINT STRING$(10, "-"):PRINT
9060 RETURN
9070 '
*/ });
